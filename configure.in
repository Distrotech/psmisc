dnl Process this file with autoconf to produce a configure script.
AC_INIT([psmisc],[21.6cvs])
AC_PREREQ(2.59)
AC_CONFIG_SRCDIR([src/comm.h])
AC_CONFIG_HEADER([config.h])
AC_CONFIG_AUX_DIR([config])
AM_INIT_AUTOMAKE([1.6])

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP

dnl checks for options
AC_SUBST([FLASK_LINUX])
AC_ARG_ENABLE([flask],
  [AS_HELP_STRING([--enable-flask], [Enable Security-Enhanced Linux features])],
  [AC_DEFINE([FLASK_LINUX], [1], [Use Security-Enhanced Linux features])
   AC_CHECK_LIB([secure], [avc_toggle], [FLASK_LIB=-lsecure], [
     AC_MSG_ERROR([Cannot find selinux/ secure static library]) ]) ],
  [])
AC_SUBST([FLASK_LIB])

dnl Checks for libraries.
AC_CHECK_LIB([ncurses], [tgetent], [TERMCAP_LIB=-lncurses], [
  AC_CHECK_LIB([termcap], [tgetent], [TERMCAP_LIB=-ltermcap], [
    AC_MSG_ERROR([Cannot find ncurses or termcap libraries]) ]) ])
AC_SUBST([TERMCAP_LIB])

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h limits.h sys/ioctl.h unistd.h termcap.h])

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_UID_T

dnl Check for language stuff
AM_GNU_GETTEXT([external])
AM_GNU_GETTEXT_VERSION([0.14.1])

dnl Checks for library functions.
AC_CHECK_FUNCS([strdup strerror strtoul])

dnl Checks for Large File System
AC_SYS_LARGEFILE

AC_CONFIG_FILES([Makefile
		 doc/Makefile
		 src/Makefile
		 icons/Makefile
		 po/Makefile.in])
AC_OUTPUT
