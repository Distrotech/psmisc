dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/comm.h)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(psmisc,21.3cvs)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AM_PROG_LIBTOOL

dnl checks for options
AC_SUBST(FLASK_LINUX)
AC_ARG_ENABLE(flask,[  --enable-flask  Enable Security-Enhanced Linux features],
	AC_DEFINE([FLASK_LINUX],1,[Use Security-Enhanced Linux features])
	AC_CHECK_LIB(secure, avc_toggle, FLASK_LIB=-lsecure,
	   AC_MSG_ERROR(Cannot find selinux/ secure static library)
	)
,)
AC_SUBST(FLASK_LIB)

dnl Checks for libraries.
AC_CHECK_LIB(ncurses, tgetent, TERMCAP_LIB=-lncurses,
  AC_CHECK_LIB(termcap, tgetent, TERMCAP_LIB=-ltermcap, 
    AC_MSG_ERROR(Cannot find ncurses or termcap libraries) ) )
AC_SUBST(TERMCAP_LIB)

dnl Checks for header files.
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h sys/ioctl.h unistd.h termcap.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_STRUCT_ST_RDEV
AC_TYPE_UID_T

dnl Check what size is ino_t int, long or long logn
AC_SUBST(INO_T_IS_LONG_LONG)
AC_SUBST(INO_T_IS_INT)
AC_CHECK_SIZEOF(ino_t)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(long long)
AC_MSG_CHECKING([what type ino_t is])
if test $ac_cv_sizeof_ino_t = $ac_cv_sizeof_int; then
  AC_DEFINE([INO_T_IS_INT],1,[Define to 1 if ino_t type is int])
  AC_MSG_RESULT([int])
fi
if test $ac_cv_sizeof_ino_t = $ac_cv_sizeof_long; then
  AC_MSG_RESULT([long])
fi
if test $ac_cv_sizeof_ino_t = $ac_cv_sizeof_long_long; then
  AC_DEFINE([INO_T_IS_LONG_LONG],1,[Define to 1 if ino_t type is long long])
  AC_MSG_RESULT([long long])
fi

dnl Check for language stuff
ALL_LINGUAS="de en fr it pt sv"
AM_GNU_GETTEXT([external])

dnl Checks for library functions.
AC_CHECK_FUNCS(strdup strerror strtoul)

dnl Checks for Large File System
AC_SYS_LARGEFILE

AC_OUTPUT(Makefile doc/Makefile src/Makefile po/Makefile.in )
